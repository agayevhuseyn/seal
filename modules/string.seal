/* String Module
 * Provides core string functions.
 * Includes symbols: ascii, char, isnum, isalpha, isalnum
 * islower, isupper, lower, upper, swapcase
 *
 * Author: Huseyn Aghayev
 * Language: Seal
 * Created: 2025-06-22
 */

include sealstring as string


$ascii = string.ascii
$char = string.char


define isnum(c)
    i = ascii(c)
    return i >= ascii('0') and i <= ascii('9')


define isalpha(c)
    i = ascii(c)
    return ((i >= ascii('a') and i <= ascii('z') or
            (i >= ascii('A') and i <= ascii('Z'))))


define isalnum(c)
    return isnum(c) or isalpha(c)


define islower(c)
    i = ascii(c)
    return i >= ascii('a') and i <= ascii('z')


define isupper(c)
    i = ascii(c)
    return i >= ascii('A') and i <= ascii('Z')


define lower(s)
    res = ''
    for c in s
        if islower(c) or !isalpha(c)
            res += c
            skip

        res += char(ascii(c) + 32)

    return res


define upper(s)
    res = ''
    for c in s
        if isupper(c) or !isalpha(c)
            res += c
            skip

        res += char(ascii(c) - 32)

    return res


define swapcase(s)
    res = ''
    for c in s
        if !isalpha(c)
            res += c
            skip

        if islower(c)
            res += char(ascii(c) - 32)
        else
            res += char(ascii(c) + 32)

    return res


define startwith(s, b)
    if (bl = len(b)) > len(s) then return false

    i = 0
    while i < bl
        if s[i] != b[i]
            return false

        i += 1

    return true


define endwith(s, e)
    if (el = len(e)) > (sl = len(s)) then return false

    i = 0
    while i < el
        if s[sl - i - 1] != e[el - i - 1]
            return false

        i += 1

    return true
